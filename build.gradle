apply plugin: 'java'
apply plugin: 'c'

sourceCompatibility = 1.6 
version = '0.1'

task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}

repositories {
	maven {
		url "http://repo.bukkit.org/content/groups/public/"
	}
	
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	compile 'org.bukkit:bukkit:1.7.9-R0.2'
	compile ':jnlua-0.9.6'
}

model {
	components {
		lua(NativeLibrarySpec) {
			sources {
				c {
					source {
						srcDir "lib/lua/src"
						include("lapi.c", "lcode.c", "ldebug.c", "ldo.c", "ldump.c", "lfunc.c", "lgc.c", "llex.c",
							"lmem.c", "lobject.c", "lopcodes.c", "lparser.c", "lstate.c", "lstring.c",
							"ltable.c", "ltm.c", "lundump.c", "lvm.c", "lzio.c",
							"lauxlib.c", "lbaselib.c", "ldblib.c", "liolib.c", "lmathlib.c", "loslib.c",
							"ltablib.c", "lstrlib.c", "loadlib.c", "linit.c")
					}
					exportedHeaders {
						srcDir "lib/lua/src"
						include "**/*.h"
					}
					binaries.withType(StaticLibraryBinarySpec) { bin ->
						binaries.remove(bin)
					}
					binaries.withType(SharedLibraryBinarySpec) { bin ->
						bin.sharedLibraryFile file("build/staging/lua5.1.dll")
						bin.sharedLibraryLinkFile bin.sharedLibraryFile
					}
				}
			}
		}

		mokapot(NativeLibrarySpec) {
			sources {
				c {
					source {
						srcDir "src/main/c"
						include "**/*.c"
					}
					//exportedHeaders {
					//	srcDir "lua-5.1/src"
					//	include "**/*.h"
					//}
					lib library: 'lua'
					binaries.withType(StaticLibraryBinarySpec) { bin ->
						binaries.remove(bin)
					}
					binaries.withType(SharedLibraryBinarySpec) { bin ->
						bin.sharedLibraryFile file("build/staging/Plugins/MokaPot/mokapot.dll")
						bin.sharedLibraryLinkFile bin.sharedLibraryFile
					}
				}
			}
		}
	}
}

jar {
	archiveName "mokapot.jar"
	destinationDir file("build/staging/Plugins/MokaPot")
}

task luaScripts(type: Copy) {
	description "Copies the lua scripts to the staging directory."
	group "build"
	from "src/main/lua"
	into "build/staging/Plugins/MokaPot"
}
assemble.dependsOn += luaScripts

check.dependsOn -= test
task test(overwrite: true) {
	dependsOn classes, testClasses
	description 'Runs the tests.'
	group 'verification'
	outputs.dir file('build/classes/test')
	inputs.dir file('src/test')
	doFirst {
		
	}
}
check.dependsOn += test